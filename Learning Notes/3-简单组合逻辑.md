## 4 简单组合逻辑

### 4.1 多路选择器

#### 4.1.1 理论学习

* (1)数字电路根据电路的不同特点可以分为：`组合逻辑`和`时序逻辑`。
* (2)组合逻辑电路的特点：输出信号只是当前时刻输入信号的函数，与其他时刻的输入状态无关，无储存电路，也没有反馈电路。
* (3)多路选择器(数据选择器)：在多路数据传送过程中，能够根据需要将其中任意一路选出来的电路，叫做数据选择器，也称多路选择器或多路开关。
  * ![image](https://github.com/user-attachments/assets/25b8df35-8cc7-44c0-83b4-1e74c72f7972)


#### 4.1.2 实战演练

* (1)实战项目：

  * 选择信号sel为高电平时，输入信号in_1的输出；
    选择信号sel为低电平时，输入信号in_2的输出。

* (2)绘制整体波形图：

  * 在“doc”文件夹中新建“visio”文件绘制波形图；
  * ![image](https://github.com/user-attachments/assets/d70564a1-a801-4f03-8cb3-e9cf38fcbd43)


* (3)编写代码

  * a.在“rtl”文件夹新建“.v”文件；

    * ``` verilog
      /****************模块开始****************/
      module  mux2_1      //module后面跟着模块名，并且模块名要与文件名尽量保持一致
      
      /****************定义变量****************/
      (
          input   wire    [0:0]   in_1,//wire后面是信号位宽，如果是1位款可以不用编写，或者用[0:0]表示
          input   wire            in_2,
          input   wire            sel ,
      
          output  reg             out  //输出信号将用于always语句中，变量要求必须为reg型变量
      );
      
      /******用组合逻辑对输出信号进行赋值******/
      always@(*)//*号相当于任何信号变化都会执行always语句，此时的*相当于in_1、in_2、out
      //always语句使用方法在视频03:00中
          if(sel == 1'b1)              //当sel为高电平时
              out = in_1;              //如果只有一条语句，begin和end可以不写
      
          else
              out = in_2;
      
      endmodule                        //每个模块只有一组module-endmodule
      ```

  * b.打开Quarts II进行代码编译

  * c.在“sim”文件夹新建“.v”测试代码文件：

    * ``` verilog
      `timescale 1ns/1ns//仿真文件必不可少：时间尺度预编译指令 时间单位/时间精度
      
      module  tb_mux2_1();//仿真测试这里的括号不用添加输入输出信号
      //由于仿真测试成语用于项目的always语句中，所以在这里的变量全部是reg型
      reg     in_1;
      reg     in_2;
      reg     sel ;
      
      wire    out ;//输出信号可以用wire方便观察
      
      initial//该语句上电只执行一次
          begin
              in_1    <=  1'b0;
              in_2    <=  1'b0;
              sel     <=  1'b0;
          end
          
      always #10 in_1 <=  {$random} % 2;//{$random} % 2随机数除2取余
                                        //要么是1要么是0，这样可以模拟高低电平，
                                        //#10表示每隔10个时间单位执行一次
      always #10 in_2 <=  {$random} % 2;
      always #10 sel  <=  {$random} % 2;
      
      initial
          begin
              $timeformat(-9,0,"ns",6);//系统函数设置显示的时间格式
                                       //此处表示的是(打印时间单位为纳秒,
                                       //小数点后打印的小数位为 0 位,
                                       //时间值后打印的字符串为“ns”,
                                       //打印的最小数量字符为 6 个)
                                       
              //只要监测的变量（时间、in1, in2, sel, out）发生变化，就会打印出相应的信息
              $monitor("@time %t:in_1=%b in_2=%b sel=%b out=%b",$time,in_1,in_2,sel,out);
          end
      /*实例化，在仿真文件中调用被仿真文件*/
      mux2_1  mux2_1_inst      //实例化1次
      
      /****************定义变量****************/
      (
          .in_1(in_1),//.表示连接,.b(a):将（被仿真变量a）与仿真变量b连接
          .in_2(in_2),
          .sel (sel),
      
          .out (out) //输出信号将用于always语句中，变量要求必须为reg型变量
      );
      endmodule
      ```

  * d.在Quartus II软件中加入仿真测试文件，并设置相关仿真参数

  * c.点击Modelsim联仿按钮进行联仿（具体操作见3.1.7）；

  * d.查看波形图进行验证

  * e.管脚绑定

    * in_1-->key1-->M2
    * in_2-->key2-->M1
    * sel  -->key3-->E15
    * out  -->led0-->L7

  * f.回到实验工程进行全编译

  * g.点击program按钮导入“.sof”文件；

  * h.连接板卡，点击“start”按钮下载程序；

  * I.上板验证；

  * g.程序固化。

#### 4.1.3 其他方法

* (1)always 中 case 实现方法

  * ``` verilog
    endmodule
    
    (
    input wire in1, //输入端 1
    input wire in2, //输入端 2
    input wire sel, //选择端
    
    output reg out //结果输出
    );
    
    //out:组合逻辑输出选择结果
    always@(*)
    case(sel)
    1'b1 : out = in1;
    1'b0 : out = in2;
    //如果 sel 不能列举出所有的情况一定要加 default
    //此处 sel 只有两种情况，并且完全列举了，所以 default 可以省略
    default : out = in1;
    endcase
    
    endmodule
    ```

  * ![image](https://github.com/user-attachments/assets/1dadf998-1ab2-45fa-8fbd-f3ad4375c33a)


* (2)assign中条件运算符（三元运算符）实现方法

  * ``` verilog
    module mux2_1
    (
    input wire in1, //输入端 1
    input wire in2, //输入端 2
    input wire sel, //选择端
    
    output wire out //结果输出
    );
    
    //out:组合逻辑输出选择结果
    //此处使用的是条件运算符（三元运算符），当括号里面的条件成立时
    //执行"?”后面的结果；如果括号里面的条件不成立时，执行“：”后面的结果
    assign out = (sel == 1'b1) ? in1 : in2;
    
    endmodule
    ```

  * ![image](https://github.com/user-attachments/assets/46cd9b88-dadf-44f2-bad1-be027ea3d06d)


### 4.2 译码器

#### 4.2.1 理论学习

* 译码器原理：译码是编码的逆过程，在编码时，每一种二进制代码，都赋予了特定的含义，即都表示了一个确定的信号或者对象。把代码状态的特定含义翻译出来的过程叫做译码，实现译码操作的电称为译码器。或者说，译码器是可以将输入二进制代码的状态翻译成输出信号，以表示其原来含义的电路。
* 译码器（decoder）是一类多输入多输出组合逻辑电路器件，其可以分为：
  * 变量译码和显示译码两类。

#### 4.2.2 实战演练

* (1)实验目标

  * 设计并仿真验证 3-8 译码器。

* (2)绘制模块框图

  * ![image](https://github.com/user-attachments/assets/46f1a101-afac-4d69-91ca-37e5851d5bd0)


* (3)绘制真值表

  * ![image](https://github.com/user-attachments/assets/98ce64cc-99a9-4344-96fb-555f32920686)


* (4)绘制波形图

  * ![image](https://github.com/user-attachments/assets/972f3165-24fd-411a-bf0e-f6a45a89fdce)


* (5)代码编写

  * 打开“rtl”文件夹新建“.v”文件；

    * if-else语句写法(存在优先级顺序执行)：

    * ``` verilog
      module  decoder
      (
          input   wire            in_1,
          input   wire            in_2,
          input   wire            in_3,
          
          output  reg     [7:0]   out//这里用reg型
                                     //因为后面需要用到always语句
                                     //设置位宽为8bit
      );
      
      always@(*)
          if({in_1,in_2,in_3} == 3'b000)//这里用{}来拼接符号
              out = 8'b0000_0001;
          else if({in_1,in_2,in_3} == 3'b001)
              out = 8'b0000_0010;
          else if({in_1,in_2,in_3} == 3'b010)
              out = 8'b0000_0100;
          else if({in_1,in_2,in_3} == 3'b011)
              out = 8'b0000_1000;
          else if({in_1,in_2,in_3} == 3'b100)
              out = 8'b0001_0000;
          else if({in_1,in_2,in_3} == 3'b101)
              out = 8'b0010_0000;
          else if({in_1,in_2,in_3} == 3'b110)
              out = 8'b0100_0000;
          else if({in_1,in_2,in_3} == 3'b111)
              out = 8'b1000_0000;
          else
              out = 8'b0000_0001;
      endmodule
      ```

    * case语句写法(不存在优先级顺序执行方式，通过判断变量执行对应指令)：

    * ``` verilog
      module  decoder
      (
          input   wire            in_1,
          input   wire            in_2,
          input   wire            in_3,
          
          output  reg     [7:0]   out//这里用reg型
                                     //因为后面需要用到always语句
                                     //设置位宽为8bit
      );
      
      always@()
          case({in_1,in_2,in_3})
              3'b000:out = 8'b0000_0001;
              3'b001:out = 8'b0000_0010;
              3'b010:out = 8'b0000_0100;
              3'b011:out = 8'b0000_1000;
              3'b100:out = 8'b0001_0000;
              3'b101:out = 8'b0010_0000;
              3'b110:out = 8'b0100_0000;
              3'b111:out = 8'b1000_0000;
              default:out = 8'b0000_0001;
          endcase
      endmodule
      ```

    * 

  * 打开Quartus II软件新建工程文件在“quartus_prj”文件夹当中，并设置好芯片型号和仿真软件；

  * 导入“rtl”文件中编写完成的“.v”文件，进行编译。

    * 这里出现的BUG：
      * a.“top_level”文件名前缀不能有数字；
        * ![image](https://github.com/user-attachments/assets/b356219a-61aa-42c3-bbbb-5db99d25ffbc)

      * b.“.v”文件前缀不能有数字；
      * c.“module”指令后面文件名前缀不能有数字。
    * 可以使用“RTL Viewer”查看代码综合出的视图。
      * ![image](https://github.com/user-attachments/assets/1b285434-a3d5-4a1a-ae46-5444c14a40ac)


* (6)测试代码编写

  * 打开“sim”文件夹新建“.v”文件；

  * ``` verilog
    `timescale 1ns/1ns
    
    module  tb_decoder();
    
    reg     in_1;
    reg     in_2;
    reg     in_3;
    
    wire    [7:0]   out;
    
    initial
        begin
            in_1 <= 1'b0;
            in_2 <= 1'b0;
            in_3 <= 1'b0;
        end
    
    always #10  in_1 <= {$random} % 2;
    always #10  in_2 <= {$random} % 2;
    always #10  in_3 <= {$random} % 2;
    
    initial
        begin
            $timeformat(-9,0,"ns",6);
            $monitor("@time %t:in_1=%b,in_2=%b,in_3=%b,out=%b",$time,in_1,in_2,in_3,out);
        end
    
    decoder decoder_inst//实例化
    (
        .in_1(in_1),
        .in_2(in_2),
        .in_3(in_3),
    
        .out (out)
    );
    
    endmodule
    ```

    * “decoder_decoder_inst”例化示意图:
      * ![image](https://github.com/user-attachments/assets/eedb5b79-f5cc-4b23-a4c0-65cdd40d8e1c)


  * 导入测试文件，进行Modelsim联仿，查看波形图和打印信息：

    * ![image](https://github.com/user-attachments/assets/bdf9bb79-b8ed-4dd7-aeca-863a772466a0)

    * ![image](https://github.com/user-attachments/assets/be699211-9e84-47b1-aeae-71459d0ec4a4)


  * 与真值表对比即可。

### 4.3 半加器

#### 4.3.1 理论学习

* 主要用于两个数或者多个数的加和， 加法器又分为半加器（half adder）和全加器（full adder）。
  * 半加器：指对两个输入数据位相加，输出一个结果位和进位，没有进位输入的加法器电路。是实现两个一位二进制数的加法运算电路。
  * 全加器：而全加器是在半加器的基础上的升级版，除了加数和被加数加和外还要加上上一级传进来的进位信号。

#### 4.3.2 实战演练

* (1)实验目标：

  * 设计并实现一个半加器，使用开发板上的按键 KEY1、KEY2 作为被加数输入，选择开发板上的 LED 灯 D6 表示相加和的输出，LED 灯 D7 表示进位输出。

* (2)绘制模块框图

  * ![image](https://github.com/user-attachments/assets/d03ab003-ffec-4680-b551-808629591e60)


* (3)绘制真值表

  * ![image](https://github.com/user-attachments/assets/0527e23b-0ba0-4716-b77f-d882ae4ecc6e)


* (4)绘制波形图

  * ![image](https://github.com/user-attachments/assets/45f6f920-21ae-40ff-8803-cb85803b121f)


* (5)代码编写

  * 打开“rtl”文件夹新建“.v”文件；

  * ``` verilog
    module  half_adder
    (
        input   wire        in_1,
        input   wire        in_2,
        
        output  wire        sum,
        output  wire        count
    );
    
    assign  {count,sum} = in_1 + in_2;  //相加之和的高位赋值给count
                                        //相加之和的低位赋值给sum
    
    endmodule
    ```

  * 打开Quartus II软件新建工程文件在“quartus_prj”文件夹当中，并设置好芯片型号和仿真软件；

  * 导入“rtl”文件中编写完成的“.v”文件，进行编译.

    * 可以使用“RTL Viewer”查看代码综合出的视图。
      * ![](https://pic.imgdb.cn/item/64e5f8fb661c6c8e542bf72e.jpg)

  * (6)测试代码编写

    * 打开“sim”文件夹新建“.v”文件；

    * ``` verilog
      `timescale  1ns/1ns
      module  tb_half_adder();
      
      reg     in_1;
      reg     in_2;
      
      wire    sum;
      wire    count;
      
      initial
          begin
              in_1 <= 1'b0;
              in_2 <= 1'b0;
          end
          
      always #10  in_1 <= {$random} % 2;
      always #10  in_2 <= {$random} % 2;
      
      initial
          begin
              $timeformat(-9,0,"ns",6);
              $monitor("@time %t:in_1=%b,in_2=%b,sum=%b,count=%b",$time,in_1,in_2,sum,count);
          end
          
      half_adder half_adder_inst
      (
          .in_1(in_1),
          .in_2(in_2),
      
          .sum(sum),
          .count(count)
      );
      
      endmodule
      ```

    * 导入测试文件，进行Modelsim仿真设置；

    * 开始联仿，查看波形图和打印信息（这节课讲述了Wire窗口的新设置操作）：

      * ![](https://pic.imgdb.cn/item/64e5fa36661c6c8e542c3f9e.jpg)
      * ![](https://pic.imgdb.cn/item/64e5fa5c661c6c8e542c469d.jpg)

    * 与真值表对比即可。

  * (7)管脚绑定

    * in_1-->key1-->M2
    * in_2-->key2-->M1
    * sum-->led1-->M6
    * count -->led0-->L7

  * (8)上板子验证

    * 点击program按钮导入“.sof”文件；
    * 连接板卡，点击“start”按钮下载程序；
    * 上板验证；

  * (9)程序固化。
