## 9 计数器

### 9.1 理论学习

* 计数器的功能：

  * 计数器在数字系统中主要是对脉冲的个数进行计数，以实现测量、计数和控制的功能，同时兼有分频功能。
* 计数器的应用：
  * 计数器在数字系统中应用广泛，如在电子计算机的控制器中对指令地址进行计数，以便顺序取出下一条指令，在运算器中作乘法、除法运算时记下加法、减法次数，又如在数字仪器中对脉冲的计数等等。
* 计数器也是在 FPGA 设计中最常用的一种`时序逻辑电路`。

### 9.2 实战演练

#### 9.2.1 实验目标

* 让计数器计数 1s 时间间隔，实现 led 灯每隔 1s 闪烁一次。

#### 9.2.2 程序设计

* （1）模块框图绘制：

  * 信号命名特点：sys表示系统，sys_xxx_n的n表示低电平有效，如果是高电平有效则不加后缀n。
  * ![image](https://github.com/user-attachments/assets/febd720d-f35d-4313-910c-46df355763a3)


* （2）绘制波形图

  * 计数器分析：
    * 系统时钟为50MHz，也就是一个周期时间为:[1/(50*10^6)Kz]s = 0.000_000_02s = 20ns，那么1秒就会计数50M个，0.5s闪烁一次，所以计数25M个复位1次，又因为计数器从0开始计数，所以0-24_999_999，计数翻转一次。所以需要25位宽的寄存器。(`考虑到资源量的问题，精简化设计`)

  * 有2种实现方法：
    * ①不带标志信号的计数器
      * 当 sys_rst_n 信号有效时，cnt 计数器清零；
      * 当 sys_rst_n 信号撤销后，时钟的上升沿时刻 cnt 计数器开始自加 1；
      * 当 cnt 计数器计数到 N（这里 N = 24_999_999）时清零，只要 sys_rst_n 不复位，该计数器将一直循环计数下去。
      * ![image](https://github.com/user-attachments/assets/98a85b34-b9b0-4836-a48a-722e1fb0af97)


    * ②带标志信号的计数器
      * 是再添加一个用于指示 cnt 计数器计数到 N 的脉冲信号 cnt_flag。
      * 当计数器计数到 N 时 led_out 信号先不取反，而是让 cnt_flag 脉冲信号产生一个时钟周期的高脉冲，led_out 信号每当检测到 cnt_flag 脉冲信号为 高时取反，也能够控制外部 led 灯实现闪烁的效果。
      * ![image](https://github.com/user-attachments/assets/be3275aa-d79b-438a-b45b-8096d630b682)

      * 脉冲标志信号（flag）的优点：
        * 可以减少代码中 if 括号内的条件让代码更加清晰简洁；
        * 当需要在多处使用脉冲标志信号的地方要比全部写出的方式更节约逻辑资源，脉冲标志信号在指示某些状态时是非常有用的。

* （5）代码编写

  * 打开“rtl”文件夹新建“.v”文件；

    * ①不带标志信号的计数器：

      * 使用参数定义常量的好处是：

        * 我们在 RTL 代码中实例化该模块时，如果需要**两个不同计数值的计数器**我们不必设计两个模块，而是**直接修改参数**的值即可。
        * 写 Testbench 进行仿真时我们也需要实例化该模块，但是我们需要仿真至少 0.5s 的时间才能够看出到 led_out 效果，这会让仿真时间很长，也会导致产生的仿真文件很大，所以`可以通过直接修改参数的方式来缩短仿真的时间而看到相同的效果`，且不会影响到 RTL 代码模块中的实际值，因为 `parameter 定义的是局部参数`，所以只在本模块中有效。为了更好的区分，`参数名`习惯上都要`大写`。

      * ``` verilog
        module  counter
        /*使用参数的方式定义常量*/
        #(parameter     CNT_MAX = 25'd24_999_999)//参数传递法可以定义在模块外部可以在实例化时候进行参数修改
        
        (
            input       wire    sys_clk     ,
            input       wire    sys_rst_n   ,
            
            output      reg     led_out
        );
        
        reg     [24:0]  cnt;
        
        //cnt:计数器计数，当计数到 CNT_MAX 的值时清零
        always@(posedge sys_clk or negedge sys_rst_n)//异步复位
            if(sys_rst_n == 1'b0)
                cnt <= 25'd0;
            else    if(cnt == CNT_MAX)
                cnt <= 25'd0;
            else
                cnt <= cnt + 1'b1;
                
        //led_out:输出控制一个 LED 灯，每当计数满标志信号有效时取反
        always@(posedge sys_clk or negedge sys_rst_n)
            if(sys_rst_n == 1'b0)
                led_out <= 1'b0;
            else    if(cnt == CNT_MAX)
                led_out <= ~led_out;
        
        endmodule
        ```

    * ②带标志信号的计数器

      * ``` verilog
        module  counter
        /*使用参数的方式定义常量*/
        #(parameter     CNT_MAX = 25'd24_999_999)//参数传递法可以定义在模块外部可以在实例化时候进行参数修改
        
        (
            input       wire    sys_clk     ,
            input       wire    sys_rst_n   ,
            
            output      reg     led_out
        );
        
        reg     [24:0]  cnt;
        reg             cnt_flag;//带标志位计数器
        //cnt:计数器计数，当计数到 CNT_MAX 的值时清零
        always@(posedge sys_clk or negedge sys_rst_n)//异步复位
            if(sys_rst_n == 1'b0)
                cnt <= 25'd0;
            else    if(cnt == CNT_MAX)
                cnt <= 25'd0;
            else
                cnt <= cnt + 25'd1;
        
        //cnt_flag：计数器标志位
        always@(posedge sys_clk or negedge sys_rst_n)//异步复位
            if(sys_rst_n == 1'b0)
                cnt_flag <= 1'b0;
            else    if(cnt == (CNT_MAX - 25'd1))
                cnt_flag <= 1'b1;
            else
                cnt_flag <= 1'b0;
        
        //led_out:输出控制一个 LED 灯，每当计数满标志信号有效时取反
        always@(posedge sys_clk or negedge sys_rst_n)
            if(sys_rst_n == 1'b0)
                led_out <= 1'b0;
            else    if(cnt_flag == 1'b1)
                led_out <= ~led_out;
            else
                led_out <= led_out;
        
        endmodule
        ```

  * 打开Quartus II软件新建工程文件在“quartus_prj”文件夹当中，并设置好芯片型号和仿真软件；

  * 导入“rtl”文件中编写完成的“.v”文件，进行编译.

    * 可以使用“RTL Viewer”查看代码综合出的视图。
      * ①不带标志信号的计数器：![](https://pic.imgdb.cn/item/64eef1f1661c6c8e54ac48d3.jpg)
      * ②带标志信号的计数器![](https://pic.imgdb.cn/item/64eeff79661c6c8e54b112b6.jpg)

#### 9.2.3 仿真验证

* (1)测试代码编写

  * 打开“sim”文件夹新建“.v”文件；

  * ``` verilog
    `timescale  1ns/1ns
    module  tb_counter();
    
    reg     sys_clk     ;
    reg     sys_rst_n   ;
    
    wire    led_out     ;
    
    //初始化输入信号
    initial
        begin
            sys_clk = 1'b1;
            sys_rst_n <= 1'b0;
            #20
            sys_rst_n <= 1'b1;
        end
    //产生时钟信号，定义下频率，每10ns翻转一次电平，20ns周期
    always  #10 sys_clk = ~sys_clk;
    
    //时间打印
    initial
        begin
            $timeformat(-9,0,"ns",6);
            $monitor("@time %t:led_out=%b",$time,led_out);
        end
    
    //实例化
    counter//添加参数传递后，实例化名称count_inst写在端口列表前
    #(
        .CNT_MAX (25'd24)
    )
    counter_inst
    (
        .sys_clk   (sys_clk),
        .sys_rst_n (sys_rst_n),
    
        .led_out   (led_out)
    );
    
    endmodule
    ```

  * 导入测试文件，进行Modelsim仿真设置；

  * 开始联仿，查看波形图和打印信息：

    * ①不带标志信号的计数器：

      ![](https://pic.imgdb.cn/item/64eef889661c6c8e54aeaf15.jpg)

      ![](https://pic.imgdb.cn/item/64eef938661c6c8e54aef055.jpg)

    * ②带标志信号的计数器

      ![](https://pic.imgdb.cn/item/64eeff5d661c6c8e54b103be.jpg)

      ![](https://pic.imgdb.cn/item/64eef938661c6c8e54aef055.jpg)

  * 与真值表对比即可。


#### 9.2.4 管脚绑定

* sys_clk-->E1
* sys_rst_n-->M15
* led_out-->led1-->L7

#### 9.2.5 上板子验证

* 点击program按钮导入“.sof”文件；
* 连接板卡，点击“start”按钮下载程序；
* 上板验证；

#### 9.2.6 程序固化

* 详情参见3.1.11

